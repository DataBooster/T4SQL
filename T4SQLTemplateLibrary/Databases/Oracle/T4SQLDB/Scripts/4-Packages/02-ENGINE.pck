CREATE OR REPLACE PACKAGE T4SQL.ENGINE IS

----------------------------------------------------------------------------------------------------
--
--	Copyright 2013 Abel Cheng
--	This source code is subject to terms and conditions of the Apache License, Version 2.0.
--	See http://www.apache.org/licenses/LICENSE-2.0.
--	All other rights reserved.
--	You must not remove this notice, or any other, from this software.
--
--	Original Author:	Abel Cheng <abelcys@gmail.com>
--	Created Date:		October 15, 2013, 11:30:05 PM
--	Primary Host:		http://t4sql.codeplex.com
--	Change Log:
--	Author				Date			Comment
--
--
--
--
--	(Keep code clean rather than complicated code plus long comments.)
--
----------------------------------------------------------------------------------------------------


PROCEDURE GET_CONFIG
(
	outPoll_Interval	OUT NUMBER
);


PROCEDURE STANDBY_PING
(
	outSwitch_To_Mode	OUT VARCHAR2
);

PROCEDURE PRIMARY_PING
(
	outSwitch_To_Mode	OUT VARCHAR2
);


PROCEDURE GET_DB_SERVER_ENV
(
	outDatabase_Platform	OUT VARCHAR2,
	outDatabase_Product		OUT VARCHAR2,
	outProduct_Version		OUT VARCHAR2,
	outServer_Name			OUT VARCHAR2
);


PROCEDURE REGISTER_TEMPLATE
(
	inFull_Name				VARCHAR2,
	inModule				NVARCHAR2,
	inAssembly_String		VARCHAR2,
	inClass_Description		NVARCHAR2
);


PROCEDURE REGISTER_TEMPLATE_SPEC
(
	inClass_Name			VARCHAR2,
	inProperty_Name			NVARCHAR2,
	inDefault_Value			NVARCHAR2,
	inLink_State			NVARCHAR2,
	inProperty_Description	NVARCHAR2,
	inProperty_Order		NUMBER
);


PROCEDURE LIST_COLUMN
(
	inTable_Name	VARCHAR2,
	RC1				OUT SYS_REFCURSOR
);


PROCEDURE GET_FOREIGN_KEY
(
	inTable_Name			VARCHAR2,
	outTable_Schema		OUT	VARCHAR2,
	outTable_Name		OUT	VARCHAR2,
	outQualified_Name	OUT	VARCHAR2,
	RC1					OUT SYS_REFCURSOR
);


PROCEDURE LIST_WORKSPACE
(
	RC1		OUT SYS_REFCURSOR
);


PROCEDURE LIST_WORKITEM
(
	inWorkitem_Table	VARCHAR2,
	RC1					OUT SYS_REFCURSOR
);


PROCEDURE LIST_WORKING_PROPERTY
(
	inProperty_Table	VARCHAR2,
	RC1					OUT SYS_REFCURSOR
);


PROCEDURE LIST_DEFAULT_PROPERTY
(
	RC1		OUT SYS_REFCURSOR
);


PROCEDURE LOG_SYS_ERROR
(
	inReference		NVARCHAR2,
	inMessage		VARCHAR
);


PROCEDURE SERVICE_RESTART;


END ENGINE;
/
CREATE OR REPLACE PACKAGE BODY T4SQL.ENGINE IS


g_Polling_Interval	NUMBER(3);


FUNCTION GET_POLL_INTERVAL
RETURN	NUMBER
AS
BEGIN
	IF g_Polling_Interval IS NULL THEN
		SELECT	NUMBER_VALUE		INTO g_Polling_Interval
		FROM	T4SQL.ENGINE_CONFIG
		WHERE	ELEMENT_NAME = 'POLL_INTERVAL';
	END IF;
	RETURN g_Polling_Interval;
END GET_POLL_INTERVAL;


PROCEDURE GET_CONFIG
(
	outPoll_Interval	OUT NUMBER
)	AS
BEGIN
	outPoll_Interval	:= GET_POLL_INTERVAL();
END GET_CONFIG;


PROCEDURE SERVICE_PING
(
	inIs_Primary	CHAR
)	AS
BEGIN
	MERGE INTO T4SQL.ENGINE_SERVER		P
	USING
	(
		SELECT
			NVL(SYS_CONTEXT('USERENV', 'TERMINAL'), '?')	AS SERVER_NAME,
			SYSTIMESTAMP									AS SERVICE_BEAT,
			inIs_Primary									AS IS_PRIMARY,
			NVL(SYS_CONTEXT('USERENV', 'OS_USER'), '?')		AS SERVICE_ACCOUNT
		FROM
			DUAL
	)	C
	ON	(P.SERVER_NAME = C.SERVER_NAME)
	WHEN MATCHED THEN
		UPDATE SET
			P.SERVICE_BEAT		= C.SERVICE_BEAT,
			P.IS_PRIMARY		= C.IS_PRIMARY,
			P.SERVICE_ACCOUNT	= C.SERVICE_ACCOUNT
	WHEN NOT MATCHED THEN
		INSERT	(P.SERVER_NAME, P.SERVICE_BEAT, P.IS_PRIMARY, P.SERVICE_ACCOUNT)
		VALUES	(C.SERVER_NAME, C.SERVICE_BEAT, C.IS_PRIMARY, C.SERVICE_ACCOUNT);
END SERVICE_PING;


PROCEDURE STANDBY_PING
(
	outSwitch_To_Mode	OUT VARCHAR2
)	AS
	tInterval		NUMBER	:= GET_POLL_INTERVAL();
	tNow			DATE	:= SYSDATE;
	tPrimary_Beat	DATE;
BEGIN
	UPDATE	T4SQL.ENGINE_CONFIG
	SET		DATE_VALUE		= tNow
	WHERE	DATE_VALUE		<= tNow - tInterval
		AND	ELEMENT_NAME	= 'STANDBY_BEAT';

	IF SQL%ROWCOUNT > 0 THEN
		SELECT DATE_VALUE INTO tPrimary_Beat FROM T4SQL.ENGINE_CONFIG WHERE ELEMENT_NAME = 'PRIMARY_BEAT';
		IF (tNow - tPrimary_Beat) > (tInterval / 2) THEN
			outSwitch_To_Mode	:= 'Primary';
		ELSE
			outSwitch_To_Mode	:= 'Standby';
		END IF;
	END IF;

	SERVICE_PING('N');
	COMMIT;
END STANDBY_PING;


PROCEDURE PRIMARY_PING
(
	outSwitch_To_Mode	OUT VARCHAR2
)	AS
	tNow	DATE	:= SYSDATE;
BEGIN
	UPDATE	T4SQL.ENGINE_CONFIG
	SET		DATE_VALUE		= tNow
	WHERE	ELEMENT_NAME	= 'PRIMARY_BEAT';

	outSwitch_To_Mode		:= 'Primary';

	SERVICE_PING('Y');
	COMMIT;
END PRIMARY_PING;


PROCEDURE GET_DB_SERVER_ENV
(
	outDatabase_Platform	OUT VARCHAR2,
	outDatabase_Product		OUT VARCHAR2,
	outProduct_Version		OUT VARCHAR2,
	outServer_Name			OUT VARCHAR2
)	AS
BEGIN
	outDatabase_Platform	:= 'Oracle';
	outServer_Name			:= SYS_CONTEXT('USERENV', 'DB_NAME');

	SELECT
		banner,
		REGEXP_SUBSTR(banner, '\d+\.\d+(\.\d+)?(\.\d+)?')
	INTO
		outDatabase_Product,
		outProduct_Version
	FROM
		v$version
	WHERE
		banner LIKE 'Oracle Database%';
END GET_DB_SERVER_ENV;


PROCEDURE REGISTER_TEMPLATE
(
	inFull_Name				VARCHAR2,
	inModule				NVARCHAR2,
	inAssembly_String		VARCHAR2,
	inClass_Description		NVARCHAR2
)	AS
BEGIN
	MERGE INTO T4SQL.TEMPLATE_CLASS		T
	USING
	(
		SELECT
			inFull_Name 			AS FULL_NAME,
			inModule				AS MODULE,
			inAssembly_String		AS ASSEMBLY_STRING,
			inClass_Description		AS CLASS_DESCRIPTION,
			SYSDATE					AS REGISTER_TIME
		FROM
			DUAL
	)	R
	ON	(T.FULL_NAME = R.FULL_NAME)
	WHEN MATCHED THEN
		UPDATE SET
			T.MODULE			= R.MODULE,
			T.ASSEMBLY_STRING	= R.ASSEMBLY_STRING,
			T.START_TIME		= R.REGISTER_TIME,
			T.IS_ACTIVE			= 'Y',
			T.CLASS_DESCRIPTION	= R.CLASS_DESCRIPTION
	WHEN NOT MATCHED THEN
		INSERT	(FULL_NAME, MODULE, ASSEMBLY_STRING, CREATED_TIME, START_TIME, IS_ACTIVE, CLASS_DESCRIPTION)
		VALUES	(R.FULL_NAME, R.MODULE, R.ASSEMBLY_STRING, R.REGISTER_TIME, R.REGISTER_TIME, 'Y', R.CLASS_DESCRIPTION);
	COMMIT;
END REGISTER_TEMPLATE;


PROCEDURE REGISTER_TEMPLATE_SPEC
(
	inClass_Name			VARCHAR2,
	inProperty_Name			NVARCHAR2,
	inDefault_Value			NVARCHAR2,
	inLink_State			NVARCHAR2,
	inProperty_Description	NVARCHAR2,
	inProperty_Order		NUMBER
)	AS
BEGIN
	MERGE INTO T4SQL.TEMPLATE_SPEC	T
	USING
	(
		SELECT
			inClass_Name			AS CLASS_NAME,
			inProperty_Name			AS PROPERTY_NAME,
			inDefault_Value			AS DEFAULT_VALUE,
			inLink_State			AS LINK_STATE,
			inProperty_Description	AS PROPERTY_DESCRIPTION,
			inProperty_Order		AS PROPERTY_ORDER
		FROM
			DUAL
	)	R
	ON	(T.PROPERTY_NAME = R.PROPERTY_NAME AND T.CLASS_NAME = R.CLASS_NAME)
	WHEN MATCHED THEN
		UPDATE SET
			T.DEFAULT_VALUE			= R.DEFAULT_VALUE,
			T.LINK_STATE			= R.LINK_STATE,
			T.PROPERTY_DESCRIPTION	= R.PROPERTY_DESCRIPTION,
			T.PROPERTY_ORDER		= R.PROPERTY_ORDER
	WHEN NOT MATCHED THEN
		INSERT	(CLASS_NAME, PROPERTY_NAME, DEFAULT_VALUE, LINK_STATE, PROPERTY_DESCRIPTION, PROPERTY_ORDER)
		VALUES	(R.CLASS_NAME, R.PROPERTY_NAME, R.DEFAULT_VALUE, R.LINK_STATE, R.PROPERTY_DESCRIPTION, R.PROPERTY_ORDER);
	COMMIT;
END REGISTER_TEMPLATE_SPEC;


PROCEDURE LIST_COLUMN
(
	inTable_Name	VARCHAR2,
	RC1				OUT SYS_REFCURSOR
)	AS
	tSchema_Name	VARCHAR2(30)	:= T4SQL.META.PARSENAME(inTable_Name, 2);
	tTable_Name		VARCHAR2(30)	:= T4SQL.META.PARSENAME(inTable_Name, 1);
BEGIN
	OPEN RC1 FOR
	SELECT
		COLUMN_NAME,
		DECODE(NULLABLE, 'Y', 'True', 'False')		AS IS_NULLABLE
	FROM
		ALL_TAB_COLUMNS
	WHERE
			TABLE_NAME	= tTable_Name
		AND	OWNER		= tSchema_Name
	ORDER BY
		COLUMN_ID;

END LIST_COLUMN;


PROCEDURE GET_FOREIGN_KEY
(
	inTable_Name			VARCHAR2,
	outTable_Schema		OUT	VARCHAR2,
	outTable_Name		OUT	VARCHAR2,
	outQualified_Name	OUT	VARCHAR2,
	RC1					OUT SYS_REFCURSOR
)	AS
BEGIN
	outTable_Schema		:= T4SQL.META.PARSENAME(inTable_Name, 2);
	outTable_Name		:= T4SQL.META.PARSENAME(inTable_Name, 1);
	outQualified_Name	:= outTable_Schema || '.' || outTable_Name;

	OPEN RC1 FOR
	SELECT
		CONSTRAINT_NAME,
		FOREIGN_KEY_COLUMN,
		DECODE(FOREIGN_KEY_NULLABLE, 'Y', 'True', 'False')		AS FOREIGN_KEY_NULLABLE,
		REFERENCED_SCHEMA || '.' || REFERENCED_TABLE			AS REFERENCED_TABLE,
		REFERENCED_COLUMN,
		DECODE(REFERENCED_NULLABLE, 'Y', 'True', 'False')		AS REFERENCED_NULLABLE
	FROM
		T4SQL.VW_FOREIGN_KEY_COLUMNS
	WHERE
			TABLE_NAME	= outTable_Name
		AND	OWNER		= outTable_Schema;

END GET_FOREIGN_KEY;


PROCEDURE LIST_WORKSPACE
(
	RC1		OUT SYS_REFCURSOR
)	AS
BEGIN
	OPEN RC1 FOR
	SELECT
		WORKITEM_TABLE_NAME,
		PROPERTY_TABLE_NAME
	FROM
		T4SQL.WORKSPACE_ENTRY
	ORDER BY
		WORKSPACE_ID;

END LIST_WORKSPACE;


PROCEDURE LIST_WORKITEM
(
	inWorkitem_Table	VARCHAR2,
	RC1					OUT SYS_REFCURSOR
)	AS
	tSQL				VARCHAR2(512);
BEGIN
	tSQL	:= 'SELECT
	I.WORKITEM_NAME,
	C.FULL_NAME		AS TEMPLATE_NAME
FROM
	T4SQL.TEMPLATE_CLASS	C,
	' || inWorkitem_Table || '	I
WHERE
		C.FULL_NAME		= I.TEMPLATE_NAME
	AND	C.IS_ACTIVE		= 1
	AND	I.START_BUILD	= 1
ORDER BY
	I.BUILD_ORDER
';

	OPEN RC1 FOR tSQL;
END LIST_WORKITEM;


PROCEDURE LIST_WORKING_PROPERTY
(
	inProperty_Table	VARCHAR2,
	RC1					OUT SYS_REFCURSOR
)	AS
	tSQL				VARCHAR2(512);
BEGIN
	tSQL	:= 'SELECT
	WORKITEM_NAME,
	PROPERTY_NAME,
	STRING_VALUE,
	LINK_STATE
FROM
	' || inProperty_Table || '
ORDER BY
	WORKITEM_NAME,
	PROPERTY_NAME
';

	OPEN RC1 FOR tSQL;
END LIST_WORKING_PROPERTY;


PROCEDURE LIST_DEFAULT_PROPERTY
(
	RC1		OUT SYS_REFCURSOR
)	AS
BEGIN
	OPEN RC1 FOR
	SELECT
		D.CLASS_NAME,
		D.PROPERTY_NAME,
		D.DEFAULT_VALUE,
		D.LINK_STATE
	FROM
		T4SQL.TEMPLATE_SPEC		D,
		T4SQL.TEMPLATE_CLASS	C
	WHERE
			D.CLASS_NAME	= C.FULL_NAME
		AND	C.IS_ACTIVE		= 'Y'
	ORDER BY
		D.PROPERTY_ORDER;

END LIST_DEFAULT_PROPERTY;


PROCEDURE LOG_SYS_ERROR
(
	inReference		NVARCHAR2,
	inMessage		VARCHAR
)	AS
BEGIN
	INSERT INTO T4SQL.EVENT_LOG (LOG_TIME, LOG_TYPE, REFERENCE_, MESSAGE_)
	VALUES (SYSTIMESTAMP, 'Error', inReference, inMessage);
	COMMIT;
END LOG_SYS_ERROR;


PROCEDURE SERVICE_RESTART
AS
BEGIN
	UPDATE	T4SQL.TEMPLATE_CLASS
	SET		IS_ACTIVE = 'N'
	WHERE	IS_ACTIVE = 'Y';
	COMMIT;
END SERVICE_RESTART;


END ENGINE;
/
